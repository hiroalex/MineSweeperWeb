<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" creationComplete="onCreationComplete()"
			   xmlns:mx="library://ns.adobe.com/flex/mx" resize="onResize()">
	<fx:Declarations>
		<!-- Placer ici les éléments non visuels (services et objets de valeur, par exemple). -->
	</fx:Declarations>
	
	
	<fx:Script>
		<![CDATA[ 
			import com.adelbarre.minesweeper.FaceButton;
			import com.adelbarre.minesweeper.LevelSelector;
			import com.adelbarre.minesweeper.MineSweeperGame;
			import com.adelbarre.minesweeper.event.GameEvent;
			import com.adelbarre.minesweeper.event.SelectorEvent;
			
			private var _mineSweeperGame:MineSweeperGame;
			private var _faceBtn:FaceButton;
			private var _selector:LevelSelector
			
			private var _mines:int=10;
			//private static const GRID_WIDTH:int=20;
			//private static const GRID_HEIGHT:int=10;
			
			private var _gridWidth:int=10;
			private var _gridHeight:int=10;
			 
			private function onCreationComplete():void
			{
				_mineSweeperGame = new MineSweeperGame(boardContainer);
				_mineSweeperGame.addEventListener(GameEvent.MINE_FLAGGED_UPDATE,onMineFlaggedUpdate);
				_mineSweeperGame.addEventListener(GameEvent.GAMEOVER,onGameOver);
				_mineSweeperGame.addEventListener(GameEvent.VICTORY,onVictory);
				_mineSweeperGame.addEventListener(GameEvent.TIMER,onTimer);
				
				
				_mineSweeperGame.start(_gridWidth,_gridHeight,_mines);
				remainingLabel.text=_mines.toString();
				timerLabel.text="0";
				
				_faceBtn=new FaceButton();
				_faceBtn.addEventListener(MouseEvent.CLICK,onFaceBtnClick);
				buttonContainer.addChild(_faceBtn);
				
				_selector=new LevelSelector();
				_selector.addEventListener(SelectorEvent.LEVEL_CHANGE,onLevelChange);
				levelSelectorContainer.addChild(_selector);
				
				
				this.minHeight=_gridHeight*MineSweeperGame.SQUARE_HEIGHT+100;
				this.minWidth=_gridWidth*MineSweeperGame.SQUARE_WIDTH+100;
			}
			
			private function onLevelChange(evt:SelectorEvent):void
			{
				switch(evt.selectedLevel)
				{
					case LevelSelector.LEVEL_EASY:
						_gridWidth=10;
						_gridHeight=10;
						_mines=10;
						//boardContainer.percentHeight=50;
						break;
					
					case LevelSelector.LEVEL_MEDIUM:
						_gridWidth=20;
						_gridHeight=12;
						_mines=25;
						//boardContainer.percentHeight=100;
						break;
					
					case LevelSelector.LEVEL_HARD:
						_gridWidth=30;
						_gridHeight=16;
						_mines=40;
						//boardContainer.percentHeight=100;
						break;
						
				}
				
				startNewGame();
			}
			
			private function onTimer(evt:GameEvent):void
			{
				timerLabel.text=evt.timerValue.toString();
			}
			
			private function onGameOver(evt:GameEvent):void
			{
				_faceBtn.setFace(FaceButton.GAMEOVER);
			}
			
			private function onVictory(evt:GameEvent):void
			{
				_faceBtn.setFace(FaceButton.VICTORY);
			}
			
			private function onFaceBtnClick(evt:MouseEvent):void
			{
				startNewGame();
			}
			
			private function startNewGame():void
			{
				remainingLabel.text=_mines.toString();
				_faceBtn.setFace(FaceButton.PLAYING);
				timerLabel.text="0";
				_mineSweeperGame.start(_gridWidth,_gridHeight,_mines);
			}
			
			private function onMineFlaggedUpdate(evt:GameEvent):void
			{
				if(remainingLabel)
				{
					remainingLabel.text=evt.remainingMines.toString();
				}
			}
			
			private function onResize():void 
			{
				if(_mineSweeperGame) _mineSweeperGame.recenter(); 
				if(_selector) _selector.recenter(); 
			}
			
		]]>
	</fx:Script>
	
	<s:VGroup width="100%" height="100%">	
		<s:Spacer height="20"/>	
		<s:HGroup  width="100%" height="40" horizontalAlign="center">			
			<s:Label id="remainingLabel" width="80" textAlign="center" fontSize="30" height="32" verticalAlign="bottom"
					 backgroundColor="0x000000" color="0xFF0000"/>
			<s:Spacer width="40"/>
			<s:SpriteVisualElement id="buttonContainer" width="30"/>
			<s:Spacer width="40"/>
			<s:Label id="timerLabel" width="80" textAlign="center" fontSize="30" height="32" verticalAlign="bottom"
					 backgroundColor="0x000000" color="0xFF0000"/>
		</s:HGroup>
		<s:Spacer height="40"/>
		<s:SpriteVisualElement width="100%" height="40" id="levelSelectorContainer"/>
		<s:SpriteVisualElement width="100%" height="400" id="boardContainer"/> 
	</s:VGroup>
	
</s:Application>
